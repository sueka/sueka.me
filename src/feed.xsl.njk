<?xml version="1.0" encoding="utf-8"?>
<xsl:transform version="2.0"
  xmlns:a="http://www.w3.org/2005/Atom"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
>
  <xsl:output encoding="utf-8" method="html" doctype-system="" />

  <xsl:strip-space elements="*" />

  <xsl:template match="/">
    <html lang="{a:feed/@xml:lang}">
      <head>
        <meta charset="UTF-8" />
        <meta name="viewport" content="width=device-width,initial-scale=1.0" />
        <meta http-equiv="X-UA-Compatible" content="IE=edge" />
        <title>
          <xsl:value-of select="a:feed/a:title/text()" />
        </title>
        <link href="{{ '~/assets/stylesheets/main.css' | url(true) }}" rel="stylesheet" />
        <link href="{{ '~/assets/stylesheets/dark.css' | url(true) }}" rel="stylesheet" media="(prefers-color-scheme: dark)" />
        <link href="{{ '~/assets/stylesheets/horizontal.css' | url(true) }}" rel="stylesheet" />
        <link href="{{ '~/assets/stylesheets/feed.css' | url(true) }}" rel="stylesheet" />
        <link href="https://cdn.jsdelivr.net/gh/highlightjs/cdn-release@11.9.0/build/styles/gradient-light.min.css" rel="stylesheet" media="(prefers-color-scheme: light)" />
        <link href="https://cdn.jsdelivr.net/gh/highlightjs/cdn-release@11.9.0/build/styles/gradient-dark.min.css" rel="stylesheet" media="(prefers-color-scheme: dark)" />
        <script src="https://cdn.jsdelivr.net/gh/highlightjs/cdn-release@11.9.0/build/highlight.min.js"></script>
        <script>hljs.highlightAll()</script>
        <link href="https://cdn.jsdelivr.net/npm/@fortawesome/fontawesome-free@6.2.1/css/all.min.css" rel="stylesheet" integrity="sha256-Z1K5uhUaJXA7Ll0XrZ/0JhX4lAtZFpT6jkKrEDT0drU=" crossorigin="anonymous" />
        <link rel="stylesheet" href="https://cdn.jsdelivr.net/npm/katex@0.16.2/dist/katex.min.css" integrity="sha384-bYdxxUwYipFNohQlHt0bjN/LCpueqWz13HufFEV1SUatKs1cm4L6fFgCi1jT643X" crossorigin="anonymous" />
        <script defer="" src="https://cdn.jsdelivr.net/npm/katex@0.16.2/dist/katex.min.js" integrity="sha384-Qsn9KnoKISj6dI8g7p1HBlNpVx0I8p1SvlwOldgi3IorMle61nQy4zEahWYtljaz" crossorigin="anonymous"></script>
        <!-- Bypassing both the Nunjucks variables and the XSLT attribute value templates  -->
        <xsl:variable name="renderKaTeX">
          renderMathInElement(document.body, { throwOnError: true })
        </xsl:variable>
        <script defer="" src="https://cdn.jsdelivr.net/npm/katex@0.16.2/dist/contrib/auto-render.min.js" integrity="sha384-+VBxd3r6XgURycqtZ117nYw44OOcIax56Z4dCRWbxyPt0Koah1uHoK0o4+/RRE05" crossorigin="anonymous" onload="{$renderKaTeX}"></script>
      </head>
      <body class="feed">
        <xsl:apply-templates />
      </body>
    </html>
  </xsl:template>

  <xsl:template match="a:feed">
    <xsl:apply-templates select="a:title" />
    <dl>
      <xsl:apply-templates select="a:*[name() != 'title'][name() != 'entry']" />
    </dl>
    <div class="entries">
      <xsl:apply-templates select="a:entry">
        <xsl:sort select="a:updated/text()" order="descending" />
      </xsl:apply-templates>
    </div>
  </xsl:template>

  <xsl:template match="a:title">
    <h1>
      <a href="{../a:link[@rel = &quot;alternate&quot;]/@href}">
        <xsl:value-of select="text()" />
      </a>
    </h1>
  </xsl:template>

  <xsl:template match="a:link/@rel">
    <xsl:text xml:space="preserve"> </xsl:text>
    <xsl:value-of select="." />
  </xsl:template>

  <xsl:template match="a:link/@href">
    <xsl:value-of select="." />
  </xsl:template>

  <xsl:template match="a:published/text()|a:updated/text()">
    <time datetime="{.}" class="published">
      <xsl:value-of select="." />
    </time>
  </xsl:template>

  <xsl:template match="a:link">
    <!-- skip -->
  </xsl:template>

  <xsl:template match="/a:feed/a:link[@rel != &quot;alternate&quot;]">
    <dt>
      <xsl:value-of select="name()" />
      <xsl:apply-templates select="@rel" />
    </dt>
    <dd>
      <xsl:apply-templates select="@href" />
    </dd>
  </xsl:template>

  <xsl:template match="a:content">
    <xsl:apply-templates />
  </xsl:template>

  <xsl:template match="a:author/a:name">
    <xsl:value-of select="text()" />
  </xsl:template>

  <xsl:template match="a:entry">
    <section class="feed-entry">
      <xsl:apply-templates select="a:title" />
      <xsl:apply-templates select="a:content" />
      <hr />
      <dl>
        <xsl:apply-templates select="a:*[name() != 'title'][name() != 'link'][name() != 'content']" />
      </dl>
    </section>
  </xsl:template>

  <xsl:template match="a:*">
    <dt>
      <xsl:value-of select="name()" />
    </dt>
    <dd>
      <xsl:apply-templates select="@*" />
      <xsl:apply-templates />
    </dd>
  </xsl:template>

  <xsl:template match="a:entry/a:content/text()">
    <div class="content">
      <xsl:value-of select="." disable-output-escaping="yes" />
    </div>
  </xsl:template>

  <!-- fallback -->
  <xsl:template match="@*" />
</xsl:transform>
