---
url: ./feed.xsl
---

<?xml version="1.0" encoding="utf-8"?>
<xsl:transform version="2.0"
  xmlns:a="http://www.w3.org/2005/Atom"
  xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
>
  <xsl:output encoding="utf-8" method="html" doctype-system="" />

  <xsl:strip-space elements="*" />

  <xsl:template match="/">
    <html lang="{a:feed/@xml:lang}">
      <head>
        <meta charset="UTF-8" />
        <meta name="viewport" content="width=device-width,initial-scale=1.0" />
        <meta http-equiv="X-UA-Compatible" content="IE=edge" />
        <title>
          <xsl:value-of select="a:feed/a:title/text()" />
        </title>
        <link href="{{ '~/assets/stylesheets/main.css' | url(true) }}" rel="stylesheet" />
        <link href="{{ '~/assets/stylesheets/dark.css' | url(true) }}" rel="stylesheet" media="(prefers-color-scheme: dark)" />
        <link href="{{ '~/assets/stylesheets/horizontal.css' | url(true) }}" rel="stylesheet" />
        <link href="{{ '~/assets/stylesheets/feed.css' | url(true) }}" rel="stylesheet" />
        <link href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.10.0/styles/base16/solarized-light.min.css" rel="stylesheet" integrity="sha512-ZW2g6Pn2pMbKSyjcA+r4Lc58kcfvOdcsTuCCTl3qz8NqVJwUtAuiN61pDoW3EEfrjwH2CPtkFWMdkzMY1idilA==" crossorigin="anonymous" referrerpolicy="no-referrer" media="(prefers-color-scheme: light)" />
        <link href="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.10.0/styles/base16/solarized-dark.min.css" rel="stylesheet" integrity="sha512-kBHeOXtsKtA97/1O3ebZzWRIwiWEOmdrylPrOo3D2+pGhq1m+1CroSOVErIlsqn1xmYowKfQNVDhsczIzeLpmg==" crossorigin="anonymous" referrerpolicy="no-referrer" media="(prefers-color-scheme: dark)" />
        <script src="https://cdnjs.cloudflare.com/ajax/libs/highlight.js/11.10.0/highlight.min.js" integrity="sha512-6yoqbrcLAHDWAdQmiRlHG4+m0g/CT/V9AGyxabG8j7Jk8j3r3K6due7oqpiRMZqcYe9WM2gPcaNNxnl2ux+3tA==" crossorigin="anonymous" referrerpolicy="no-referrer"></script>
        <script>hljs.highlightAll()</script>
        <link href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/6.6.0/css/all.min.css" rel="stylesheet" integrity="sha512-Kc323vGBEqzTmouAECnVceyQqyqdsSiqLQISBL29aUW4U/M7pSPA/gEUZQqv1cwx4OnYxTxve5UMg5GT6L4JJg==" crossorigin="anonymous" referrerpolicy="no-referrer" />
        <link href="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.16.9/katex.min.css" rel="stylesheet" integrity="sha512-fHwaWebuwA7NSF5Qg/af4UeDx9XqUpYpOGgubo3yWu+b2IQR4UeQwbb42Ti7gVAjNtVoI/I9TEoYeu9omwcC6g==" crossorigin="anonymous" referrerpolicy="no-referrer" />
        <script defer="" src="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.16.9/katex.min.js" integrity="sha512-LQNxIMR5rXv7o+b1l8+N1EZMfhG7iFZ9HhnbJkTp4zjNr5Wvst75AqUeFDxeRUa7l5vEDyUiAip//r+EFLLCyA==" crossorigin="anonymous" referrerpolicy="no-referrer"></script>
        <!-- Bypassing both the Nunjucks variables and the XSLT attribute value templates  -->
        <xsl:variable name="renderKaTeX">
          renderMathInElement(document.body, { throwOnError: true })
        </xsl:variable>
        <script defer="" src="https://cdnjs.cloudflare.com/ajax/libs/KaTeX/0.16.9/contrib/auto-render.min.js" integrity="sha512-iWiuBS5nt6r60fCz26Nd0Zqe0nbk1ZTIQbl3Kv7kYsX+yKMUFHzjaH2+AnM6vp2Xs+gNmaBAVWJjSmuPw76Efg==" crossorigin="anonymous" referrerpolicy="no-referrer" onload="{$renderKaTeX}"></script>
      </head>
      <body class="feed">
        <xsl:apply-templates />
      </body>
    </html>
  </xsl:template>

  <xsl:template match="a:feed">
    <xsl:apply-templates select="a:title" />
    <dl>
      <xsl:apply-templates select="a:*[name() != 'title'][name() != 'entry']" />
    </dl>
    <div class="entries">
      <xsl:apply-templates select="a:entry">
        <xsl:sort select="a:updated/text()" order="descending" />
      </xsl:apply-templates>
    </div>
  </xsl:template>

  <xsl:template match="a:title">
    <h1>
      <a href="{../a:link[@rel = &quot;alternate&quot;]/@href}">
        <xsl:value-of select="text()" />
      </a>
    </h1>
  </xsl:template>

  <xsl:template match="a:link/@rel">
    <xsl:text xml:space="preserve"> </xsl:text>
    <xsl:value-of select="." />
  </xsl:template>

  <xsl:template match="a:link/@href">
    <xsl:value-of select="." />
  </xsl:template>

  <xsl:template match="a:published/text()|a:updated/text()">
    <time datetime="{.}" class="published">
      <xsl:value-of select="." />
    </time>
  </xsl:template>

  <xsl:template match="a:link">
    <!-- skip -->
  </xsl:template>

  <xsl:template match="/a:feed/a:link[@rel != &quot;alternate&quot;]">
    <dt>
      <xsl:value-of select="name()" />
      <xsl:apply-templates select="@rel" />
    </dt>
    <dd>
      <xsl:apply-templates select="@href" />
    </dd>
  </xsl:template>

  <xsl:template match="a:content">
    <xsl:apply-templates />
  </xsl:template>

  <xsl:template match="a:author/a:name">
    <xsl:value-of select="text()" />
  </xsl:template>

  <xsl:template match="a:entry">
    <section class="feed-entry">
      <xsl:apply-templates select="a:title" />
      <xsl:apply-templates select="a:content" />
      <hr />
      <dl>
        <xsl:apply-templates select="a:*[name() != 'title'][name() != 'link'][name() != 'content']" />
      </dl>
    </section>
  </xsl:template>

  <xsl:template match="a:*">
    <dt>
      <xsl:value-of select="name()" />
    </dt>
    <dd>
      <xsl:apply-templates select="@*" />
      <xsl:apply-templates />
    </dd>
  </xsl:template>

  <xsl:template match="a:entry/a:content/text()">
    <div class="content">
      <xsl:value-of select="." disable-output-escaping="yes" />
    </div>
  </xsl:template>

  <!-- fallback -->
  <xsl:template match="@*" />
</xsl:transform>
